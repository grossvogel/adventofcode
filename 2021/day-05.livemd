# Day 5 - Hydrothermal Venture

## Puzzle 1

You come across a field of hydrothermal vents on the ocean floor! These vents constantly produce large, opaque clouds, so it would be best to avoid them if possible.

They tend to form in lines; the submarine helpfully produces a list of nearby lines of vents (your puzzle input) for you to review. For example:

```
0,9 -> 5,9
8,0 -> 0,8
9,4 -> 3,4
2,2 -> 2,1
7,0 -> 7,4
6,4 -> 2,0
0,9 -> 2,9
3,4 -> 1,4
0,0 -> 8,8
5,5 -> 8,2
```

Each line of vents is given as a line segment in the format x1,y1 -> x2,y2 where x1,y1 are the coordinates of one end the line segment and x2,y2 are the coordinates of the other end. These line segments include the points at both ends. In other words:

An entry like 1,1 -> 1,3 covers points 1,1, 1,2, and 1,3.
An entry like 9,7 -> 7,7 covers points 9,7, 8,7, and 7,7.
For now, only consider horizontal and vertical lines: lines where either x1 = x2 or y1 = y2.

So, the horizontal and vertical lines from the above list would produce the following diagram:

```
.......1..
..1....1..
..1....1..
.......1..
.112111211
..........
..........
..........
..........
222111....
```

In this diagram, the top left corner is 0,0 and the bottom right corner is 9,9. Each position is shown as the number of lines which cover that point or . if no line covers that point. The top-left pair of 1s, for example, comes from 2,2 -> 2,1; the very bottom row is formed by the overlapping lines 0,9 -> 5,9 and 0,9 -> 2,9.

To avoid the most dangerous areas, you need to determine the number of points where at least two lines overlap. In the above example, this is anywhere in the diagram with a 2 or larger - a total of 5 points.

Consider only horizontal and vertical lines. At how many points do at least two lines overlap?

```elixir
sample = """
0,9 -> 5,9
8,0 -> 0,8
9,4 -> 3,4
2,2 -> 2,1
7,0 -> 7,4
6,4 -> 2,0
0,9 -> 2,9
3,4 -> 1,4
0,0 -> 8,8
5,5 -> 8,2
"""

input = File.read!("./input-05.txt")
```

```elixir
defmodule Line do
  defstruct from: nil, to: nil

  def load_lines(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&parse_line/1)
  end

  def parse_line(raw) do
    [from, to] =
      raw
      |> String.split(" -> ", trim: true)
      |> Enum.map(&parse_point/1)

    %__MODULE__{from: from, to: to}
  end

  def parse_point(raw) do
    [x, y] =
      raw
      |> String.split(",", trim: true)
      |> Enum.map(&String.to_integer/1)

    {x, y}
  end

  def horizontal?(%__MODULE__{from: {_, y}, to: {_, y}}), do: true
  def horizontal?(_), do: false

  def vertical?(%__MODULE__{from: {x, _}, to: {x, _}}), do: true
  def vertical?(_), do: false

  def diagonal?(line), do: !horizontal?(line) && !vertical?(line)

  def x_diff(%__MODULE__{from: {x1, _}, to: {x2, _}}), do: x2 - x1

  def y_diff(%__MODULE__{from: {_, y1}, to: {_, y2}}), do: y2 - y1

  def all_points(%__MODULE__{from: {x1, y1}} = line) do
    x = x_diff(line)
    y = y_diff(line)
    distance = [x, y] |> Enum.map(&abs/1) |> Enum.max()
    x_step = x / distance
    y_step = y / distance

    0..distance
    |> Enum.map(fn distance -> {x1 + x_step * distance, y1 + y_step * distance} end)
  end
end

defmodule Puzzle1 do
  def solve(input) do
    input
    |> Line.load_lines()
    |> Enum.filter(&(!Line.diagonal?(&1)))
    |> Enum.flat_map(&Line.all_points/1)
    |> Enum.reduce(%{}, fn point, point_map ->
      Map.update(point_map, point, 1, &(&1 + 1))
    end)
    |> Enum.filter(fn {_point, count} -> count > 1 end)
    |> Enum.count()
  end
end

Puzzle1.solve(sample)
```

```elixir
Puzzle1.solve(input)
```

## Puzzle 2

Unfortunately, considering only horizontal and vertical lines doesn't give you the full picture; you need to also consider diagonal lines.

Because of the limits of the hydrothermal vent mapping system, the lines in your list will only ever be horizontal, vertical, or a diagonal line at exactly 45 degrees. In other words:

An entry like 1,1 -> 3,3 covers points 1,1, 2,2, and 3,3.
An entry like 9,7 -> 7,9 covers points 9,7, 8,8, and 7,9.
Considering all lines from the above example would now produce the following diagram:

```
1.1....11.
.111...2..
..2.1.111.
...1.2.2..
.112313211
...1.2....
..1...1...
.1.....1..
1.......1.
222111....
```

You still need to determine the number of points where at least two lines overlap. In the above example, this is still anywhere in the diagram with a 2 or larger - now a total of 12 points.

Consider all of the lines. At how many points do at least two lines overlap?

```elixir
defmodule Puzzle2 do
  def solve(input) do
    input
    |> Line.load_lines()
    |> Enum.flat_map(&Line.all_points/1)
    |> Enum.reduce(%{}, fn point, point_map ->
      Map.update(point_map, point, 1, &(&1 + 1))
    end)
    |> Enum.filter(fn {_point, count} -> count > 1 end)
    |> Enum.count()
  end
end

Puzzle2.solve(sample)
```

```elixir
Puzzle2.solve(input)
```
