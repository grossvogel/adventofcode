# Day 13 - Transparent Origami

## Puzzle 1

You reach another volcanically active part of the cave. It would be nice if you could do some kind of thermal imaging so you could tell ahead of time which caves are too hot to safely enter.

Fortunately, the submarine seems to be equipped with a thermal camera! When you activate it, you are greeted with:

Congratulations on your purchase! To activate this infrared thermal imaging
camera system, please enter the code found on page 1 of the manual.
Apparently, the Elves have never used this feature. To your surprise, you manage to find the manual; as you go to open it, page 1 falls out. It's a large sheet of transparent paper! The transparent paper is marked with random dots and includes instructions on how to fold it up (your puzzle input). For example:

```
6,10
0,14
9,10
0,3
10,4
4,11
6,0
6,12
4,1
0,13
10,12
3,4
3,0
8,4
1,10
2,14
8,10
9,0
fold along y=7
fold along x=5
```

The first section is a list of dots on the transparent paper. 0,0 represents the top-left coordinate. The first value, x, increases to the right. The second value, y, increases downward. So, the coordinate 3,0 is to the right of 0,0, and the coordinate 0,7 is below 0,0. The coordinates in this example form the following pattern, where # is a dot on the paper and . is an empty, unmarked position:

```
...#..#..#.
....#......
...........
#..........
...#....#.#
...........
...........
...........
...........
...........
.#....#.##.
....#......
......#...#
#..........
#.#........
```

Then, there is a list of fold instructions. Each instruction indicates a line on the transparent paper and wants you to fold the paper up (for horizontal y=... lines) or left (for vertical x=... lines). In this example, the first fold instruction is fold along y=7, which designates the line formed by all of the positions where y is 7 (marked here with -):

```
...#..#..#.
....#......
...........
#..........
...#....#.#
...........
...........
-----------
...........
...........
.#....#.##.
....#......
......#...#
#..........
#.#........
```

Because this is a horizontal line, fold the bottom half up. Some of the dots might end up overlapping after the fold is complete, but dots will never appear exactly on a fold line. The result of doing this fold looks like this:

```
#.##..#..#.
#...#......
......#...#
#...#......
.#.#..#.###
...........
...........
```

Now, only 17 dots are visible.

Notice, for example, the two dots in the bottom left corner before the transparent paper is folded; after the fold is complete, those dots appear in the top left corner (at 0,0 and 0,1). Because the paper is transparent, the dot just below them in the result (at 0,3) remains visible, as it can be seen through the transparent paper.

Also notice that some dots can end up overlapping; in this case, the dots merge together and become a single dot.

The second fold instruction is fold along x=5, which indicates this line:

```
#.##.|#..#.
#...#|.....
.....|#...#
#...#|.....
.#.#.|#.###
.....|.....
.....|.....
```

Because this is a vertical line, fold left:

```
#####
#...#
#...#
#...#
#####
.....
.....
```

The instructions made a square!

The transparent paper is pretty big, so for now, focus on just completing the first fold. After the first fold in the example above, 17 dots are visible - dots that end up overlapping after the fold is completed count as a single dot.

How many dots are visible after completing just the first fold instruction on your transparent paper?

```elixir
sample = """
6,10
0,14
9,10
0,3
10,4
4,11
6,0
6,12
4,1
0,13
10,12
3,4
3,0
8,4
1,10
2,14
8,10
9,0
fold along y=7
fold along x=5
"""

input = File.read!("./input-13.txt")
```

```elixir
defmodule Origami do
  defstruct points: MapSet.new(), folds: []

  def parse(input) do
    origami =
      input
      |> String.split("\n", trim: true)
      |> Enum.reduce(%__MODULE__{}, fn raw_line, origami -> parse_line(origami, raw_line) end)

    %{origami | folds: Enum.reverse(origami.folds)}
  end

  def parse_line(
        %__MODULE__{folds: folds} = origami,
        "fold along " <> <<axis_raw>> <> "=" <> point
      ) do
    axis = if <<axis_raw>> == "x", do: :x, else: :y

    %{origami | folds: [{axis, String.to_integer(point)} | folds]}
  end

  def parse_line(%__MODULE__{points: points} = origami, coords) do
    [x, y] = String.split(coords, ",", trim: true)

    %{origami | points: MapSet.put(points, {String.to_integer(x), String.to_integer(y)})}
  end

  def apply_all_folds(%__MODULE__{folds: []} = origami), do: origami

  def apply_all_folds(origami) do
    origami |> apply_fold() |> apply_all_folds()
  end

  def apply_fold(%__MODULE__{folds: [fold | rest]} = original) do
    Enum.reduce(original.points, %{original | folds: rest}, fn point, origami ->
      new_points = origami.points |> MapSet.delete(point) |> MapSet.put(apply_fold(point, fold))
      %{origami | points: new_points}
    end)
  end

  def apply_fold({x, y}, {:x, axis}) when x > axis, do: {2 * axis - x, y}
  def apply_fold({x, y}, {:y, axis}) when y > axis, do: {x, 2 * axis - y}
  def apply_fold(point, _fold), do: point

  def point_count(%__MODULE__{points: points}), do: MapSet.size(points)

  def to_string(%__MODULE__{points: points}) do
    {max_x, max_y} =
      points
      |> MapSet.to_list()
      |> Enum.reduce({0, 0}, fn {x, y}, {accum_x, accum_y} ->
        {Enum.max([x, accum_x]), Enum.max([y, accum_y])}
      end)

    0..max_y
    |> Enum.map(fn y ->
      Enum.reduce(0..max_x, "", fn x, line ->
        entry = if MapSet.member?(points, {x, y}), do: "#", else: " "
        line <> entry
      end)
    end)
    |> Enum.join("\n")
  end
end

sample |> Origami.parse() |> Origami.apply_fold() |> Origami.point_count()
```

```elixir
input |> Origami.parse() |> Origami.apply_fold() |> Origami.point_count()
```

## Puzzle 2

Finish folding the transparent paper according to the instructions. The manual says the code is always eight capital letters.

What code do you use to activate the infrared thermal imaging camera system?

```elixir
sample |> Origami.parse() |> Origami.apply_all_folds() |> Origami.to_string() |> IO.puts()
```

```elixir
input |> Origami.parse() |> Origami.apply_all_folds() |> Origami.to_string() |> IO.puts()
```
