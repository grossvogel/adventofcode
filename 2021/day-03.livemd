# Day 3 - Binary Diagnostic

## Puzzle 1

--- Day 3: Binary Diagnostic ---
The submarine has been making some odd creaking noises, so you ask it to produce a diagnostic report just in case.

The diagnostic report (your puzzle input) consists of a list of binary numbers which, when decoded properly, can tell you many useful things about the conditions of the submarine. The first parameter to check is the power consumption.

You need to use the binary numbers in the diagnostic report to generate two new binary numbers (called the gamma rate and the epsilon rate). The power consumption can then be found by multiplying the gamma rate by the epsilon rate.

Each bit in the gamma rate can be determined by finding the most common bit in the corresponding position of all numbers in the diagnostic report. For example, given the following diagnostic report:

```
00100
11110
10110
10111
10101
01111
00111
11100
10000
11001
00010
01010
```

Considering only the first bit of each number, there are five 0 bits and seven 1 bits. Since the most common bit is 1, the first bit of the gamma rate is 1.

The most common second bit of the numbers in the diagnostic report is 0, so the second bit of the gamma rate is 0.

The most common value of the third, fourth, and fifth bits are 1, 1, and 0, respectively, and so the final three bits of the gamma rate are 110.

So, the gamma rate is the binary number 10110, or 22 in decimal.

The epsilon rate is calculated in a similar way; rather than use the most common bit, the least common bit from each position is used. So, the epsilon rate is 01001, or 9 in decimal. Multiplying the gamma rate (22) by the epsilon rate (9) produces the power consumption, 198.

Use the binary numbers in your diagnostic report to calculate the gamma rate and epsilon rate, then multiply them together. What is the power consumption of the submarine? (Be sure to represent your answer in decimal, not binary.)

```elixir
sample = [
  "00100",
  "11110",
  "10110",
  "10111",
  "10101",
  "01111",
  "00111",
  "11100",
  "10000",
  "11001",
  "00010",
  "01010"
]
```

```elixir
input = File.read!("./input-03.txt") |> String.split("\n", trim: true)
```

```elixir
Mix.install([
  {:nx, "~> 0.1.0-dev", github: "elixir-nx/nx", sparse: "nx", override: true}
])
```

```elixir
defmodule Puzzle1 do
  def energy_consumption(input_raw) do
    input_tensor = parse_input(input_raw)
    gamma(input_tensor) * epsilon(input_tensor)
  end

  def parse_input(input_raw) do
    input_raw
    |> Enum.map(fn line ->
      String.to_charlist(line) |> Enum.map(&(&1 - 48))
    end)
    |> Nx.tensor()
  end

  def gamma(input_tensor) do
    input_tensor |> Nx.sum(axes: [0]) |> Nx.greater(half(input_tensor)) |> to_decimal()
  end

  def epsilon(input_tensor) do
    input_tensor |> Nx.sum(axes: [0]) |> Nx.less(half(input_tensor)) |> to_decimal()
  end

  def half(input_tensor) do
    {rows, _row_size} = Nx.shape(input_tensor)
    Nx.divide(rows, Nx.tensor([2]))
  end

  def to_decimal(one_d_tensor) do
    one_d_tensor
    |> Nx.shape()
    |> Nx.iota()
    |> Nx.reverse()
    |> then(fn exponents -> Nx.power(2, exponents) end)
    |> Nx.multiply(one_d_tensor)
    |> Nx.sum()
    |> Nx.to_scalar()
  end
end

sample |> Puzzle1.energy_consumption() |> then(fn value -> IO.puts("Sample result: #{value}") end)
input |> Puzzle1.energy_consumption() |> then(fn value -> IO.puts("Input result: #{value}") end)
```

## Puzzle 2

Next, you should verify the life support rating, which can be determined by multiplying the oxygen generator rating by the CO2 scrubber rating.

Both the oxygen generator rating and the CO2 scrubber rating are values that can be found in your diagnostic report - finding them is the tricky part. Both values are located using a similar process that involves filtering out values until only one remains. Before searching for either rating value, start with the full list of binary numbers from your diagnostic report and consider just the first bit of those numbers. Then:

Keep only numbers selected by the bit criteria for the type of rating value for which you are searching. Discard numbers which do not match the bit criteria.
If you only have one number left, stop; this is the rating value for which you are searching.
Otherwise, repeat the process, considering the next bit to the right.
The bit criteria depends on which type of rating value you want to find:

To find oxygen generator rating, determine the most common value (0 or 1) in the current bit position, and keep only numbers with that bit in that position. If 0 and 1 are equally common, keep values with a 1 in the position being considered.
To find CO2 scrubber rating, determine the least common value (0 or 1) in the current bit position, and keep only numbers with that bit in that position. If 0 and 1 are equally common, keep values with a 0 in the position being considered.
For example, to determine the oxygen generator rating value using the same example diagnostic report from above:

Start with all 12 numbers and consider only the first bit of each number. There are more 1 bits (7) than 0 bits (5), so keep only the 7 numbers with a 1 in the first position: 11110, 10110, 10111, 10101, 11100, 10000, and 11001.
Then, consider the second bit of the 7 remaining numbers: there are more 0 bits (4) than 1 bits (3), so keep only the 4 numbers with a 0 in the second position: 10110, 10111, 10101, and 10000.
In the third position, three of the four numbers have a 1, so keep those three: 10110, 10111, and 10101.
In the fourth position, two of the three numbers have a 1, so keep those two: 10110 and 10111.
In the fifth position, there are an equal number of 0 bits and 1 bits (one each). So, to find the oxygen generator rating, keep the number with a 1 in that position: 10111.
As there is only one number left, stop; the oxygen generator rating is 10111, or 23 in decimal.
Then, to determine the CO2 scrubber rating value from the same example above:

Start again with all 12 numbers and consider only the first bit of each number. There are fewer 0 bits (5) than 1 bits (7), so keep only the 5 numbers with a 0 in the first position: 00100, 01111, 00111, 00010, and 01010.
Then, consider the second bit of the 5 remaining numbers: there are fewer 1 bits (2) than 0 bits (3), so keep only the 2 numbers with a 1 in the second position: 01111 and 01010.
In the third position, there are an equal number of 0 bits and 1 bits (one each). So, to find the CO2 scrubber rating, keep the number with a 0 in that position: 01010.
As there is only one number left, stop; the CO2 scrubber rating is 01010, or 10 in decimal.
Finally, to find the life support rating, multiply the oxygen generator rating (23) by the CO2 scrubber rating (10) to get 230.

Use the binary numbers in your diagnostic report to calculate the oxygen generator rating and CO2 scrubber rating, then multiply them together. What is the life support rating of the submarine? (Be sure to represent your answer in decimal, not binary.)

```elixir
defmodule Puzzle2 do
  use Bitwise, only_operators: true

  def life_support(raw_input) do
    # each record becomes {original_binary, int_created_by_reversing_bits}
    # the reason for reversing the bits is so we can always AND with 1 to find
    # the status of the bit we care about, then right-shift to advance
    # which bit we're looking at
    input_tuples =
      Enum.map(
        raw_input,
        fn binary ->
          {binary, binary |> String.reverse() |> to_decimal()}
        end
      )

    oxygen(input_tuples) * co2(input_tuples)
  end

  def oxygen([{binary, _}]), do: to_decimal(binary)

  def oxygen(input_tuples) do
    selector_bit = most_common_first_bit(input_tuples)
    filter_by_first_bit(input_tuples, selector_bit, &oxygen/1)
  end

  def co2([{binary, _}]), do: to_decimal(binary)

  def co2(input_tuples) do
    selector_bit = input_tuples |> most_common_first_bit() |> Bitwise.bxor(1)
    filter_by_first_bit(input_tuples, selector_bit, &co2/1)
  end

  def filter_by_first_bit(input_tuples, selector_bit, next) do
    input_tuples
    |> Enum.filter(fn {_binary, int} -> (Bitwise.bxor(selector_bit, int) &&& 1) == 0 end)
    |> Enum.map(fn {binary, int} -> {binary, int >>> 1} end)
    |> next.()
  end

  def most_common_first_bit(input_tuples) do
    if first_bit_set_count(input_tuples) >= Enum.count(input_tuples) / 2,
      do: 1,
      else: 0
  end

  # number of inputs where the first bit of the int part is a 1
  def first_bit_set_count(input_tuples) do
    Enum.count(input_tuples, &((elem(&1, 1) &&& 1) == 1))
  end

  def to_decimal(bits) do
    bits
    |> String.to_charlist()
    |> Enum.map(&(&1 - 48))
    |> Enum.reverse()
    |> Enum.with_index()
    |> Enum.map(fn {bit, index} -> bit * Integer.pow(2, index) end)
    |> Enum.sum()
  end
end

sample |> Puzzle2.life_support() |> then(fn value -> IO.puts("Sample result: #{value}") end)
input |> Puzzle2.life_support() |> then(fn value -> IO.puts("Input result: #{value}") end)
```
