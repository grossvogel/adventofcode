# Day 8 - Seven Segment Search

## Puzzle 1

You barely reach the safety of the cave when the whale smashes into the cave mouth, collapsing it. Sensors indicate another exit to this cave at a much greater depth, so you have no choice but to press on.

As your submarine slowly makes its way through the cave system, you notice that the four-digit seven-segment displays in your submarine are malfunctioning; they must have been damaged during the escape. You'll be in a lot of trouble without them, so you'd better figure out what's wrong.

Each digit of a seven-segment display is rendered by turning on or off any of seven segments named a through g:

```
  0:      1:      2:      3:      4:
 aaaa    ....    aaaa    aaaa    ....
b    c  .    c  .    c  .    c  b    c
b    c  .    c  .    c  .    c  b    c
 ....    ....    dddd    dddd    dddd
e    f  .    f  e    .  .    f  .    f
e    f  .    f  e    .  .    f  .    f
 gggg    ....    gggg    gggg    ....

  5:      6:      7:      8:      9:
 aaaa    aaaa    aaaa    aaaa    aaaa
b    .  b    .  .    c  b    c  b    c
b    .  b    .  .    c  b    c  b    c
 dddd    dddd    ....    dddd    dddd
.    f  e    f  .    f  e    f  .    f
.    f  e    f  .    f  e    f  .    f
 gggg    gggg    ....    gggg    gggg
```

So, to render a 1, only segments c and f would be turned on; the rest would be off. To render a 7, only segments a, c, and f would be turned on.

The problem is that the signals which control the segments have been mixed up on each display. The submarine is still trying to display numbers by producing output on signal wires a through g, but those wires are connected to segments randomly. Worse, the wire/segment connections are mixed up separately for each four-digit display! (All of the digits within a display use the same connections, though.)

So, you might know that only signal wires b and g are turned on, but that doesn't mean segments b and g are turned on: the only digit that uses two segments is 1, so it must mean segments c and f are meant to be on. With just that information, you still can't tell which wire (b/g) goes to which segment (c/f). For that, you'll need to collect more information.

For each display, you watch the changing signals for a while, make a note of all ten unique signal patterns you see, and then write down a single four digit output value (your puzzle input). Using the signal patterns, you should be able to work out which pattern corresponds to which digit.

For example, here is what you might see in a single entry in your notes:

```
acedgfb cdfbe gcdfa fbcad dab cefabd cdfgeb eafb cagedb ab |
cdfeb fcadb cdfeb cdbaf
```

(The entry is wrapped here to two lines so it fits; in your notes, it will all be on a single line.)

Each entry consists of ten unique signal patterns, a | delimiter, and finally the four digit output value. Within an entry, the same wire/segment connections are used (but you don't know what the connections actually are). The unique signal patterns correspond to the ten different ways the submarine tries to render a digit using the current wire/segment connections. Because 7 is the only digit that uses three segments, dab in the above example means that to render a 7, signal lines d, a, and b are on. Because 4 is the only digit that uses four segments, eafb means that to render a 4, signal lines e, a, f, and b are on.

Using this information, you should be able to work out which combination of signal wires corresponds to each of the ten digits. Then, you can decode the four digit output value. Unfortunately, in the above example, all of the digits in the output value (cdfeb fcadb cdfeb cdbaf) use five segments and are more difficult to deduce.

For now, focus on the easy digits. Consider this larger example:

```
be cfbegad cbdgef fgaecd cgeb fdcge agebfd fecdb fabcd edb |
fdgacbe cefdb cefbgd gcbe
edbfga begcd cbg gc gcadebf fbgde acbgfd abcde gfcbed gfec |
fcgedb cgb dgebacf gc
fgaebd cg bdaec gdafb agbcfd gdcbef bgcad gfac gcb cdgabef |
cg cg fdcagb cbg
fbegcd cbd adcefb dageb afcb bc aefdc ecdab fgdeca fcdbega |
efabcd cedba gadfec cb
aecbfdg fbg gf bafeg dbefa fcge gcbea fcaegb dgceab fcbdga |
gecf egdcabf bgf bfgea
fgeab ca afcebg bdacfeg cfaedg gcfdb baec bfadeg bafgc acf |
gebdcfa ecba ca fadegcb
dbcfg fgd bdegcaf fgec aegbdf ecdfab fbedc dacgb gdcebf gf |
cefg dcbef fcge gbcadfe
bdfegc cbegaf gecbf dfcage bdacg ed bedf ced adcbefg gebcd |
ed bcgafe cdgba cbgef
egadfb cdbfeg cegd fecab cgb gbdefca cg fgcdab egfdb bfceg |
gbdfcae bgc cg cgb
gcafb gcf dcaebfg ecagb gf abcdeg gaef cafbge fdbac fegbdc |
fgae cfgab fg bagce
```

Because the digits 1, 4, 7, and 8 each use a unique number of segments, you should be able to tell which combinations of signals correspond to those digits. Counting only digits in the output values (the part after | on each line), in the above example, there are 26 instances of digits that use a unique number of segments (highlighted above).

In the output values, how many times do digits 1, 4, 7, or 8 appear?

```elixir
sample = """
be cfbegad cbdgef fgaecd cgeb fdcge agebfd fecdb fabcd edb | fdgacbe cefdb cefbgd gcbe
edbfga begcd cbg gc gcadebf fbgde acbgfd abcde gfcbed gfec | fcgedb cgb dgebacf gc
fgaebd cg bdaec gdafb agbcfd gdcbef bgcad gfac gcb cdgabef | cg cg fdcagb cbg
fbegcd cbd adcefb dageb afcb bc aefdc ecdab fgdeca fcdbega | efabcd cedba gadfec cb
aecbfdg fbg gf bafeg dbefa fcge gcbea fcaegb dgceab fcbdga | gecf egdcabf bgf bfgea
fgeab ca afcebg bdacfeg cfaedg gcfdb baec bfadeg bafgc acf | gebdcfa ecba ca fadegcb
dbcfg fgd bdegcaf fgec aegbdf ecdfab fbedc dacgb gdcebf gf | cefg dcbef fcge gbcadfe
bdfegc cbegaf gecbf dfcage bdacg ed bedf ced adcbefg gebcd | ed bcgafe cdgba cbgef
egadfb cdbfeg cegd fecab cgb gbdefca cg fgcdab egfdb bfceg | gbdfcae bgc cg cgb
gcafb gcf dcaebfg ecagb gf abcdeg gaef cafbge fdbac fegbdc | fgae cfgab fg bagce
"""

input = File.read!("./input-08.txt")
```

```elixir
defmodule SevenSegment do
  @unique_segment_counts [2, 3, 4, 7]

  defstruct digits: [], display: [], solutions: %{}

  def parse(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&parse_line/1)
  end

  def puzzle1(input) do
    input
    |> parse()
    |> Enum.map(fn entry ->
      entry
      |> Map.get(:display)
      |> Enum.count(fn digit -> segment_count(digit) in @unique_segment_counts end)
    end)
    |> Enum.sum()
  end

  def puzzle2(input) do
    input
    |> parse()
    |> Enum.map(&solve_entry/1)
    |> Enum.sum()
  end

  def solve_entry(%__MODULE__{} = entry) do
    entry
    |> solve_digit(1, fn digit, _ -> segment_count(digit) == 2 end)
    |> solve_digit(7, fn digit, _ -> segment_count(digit) == 3 end)
    |> solve_digit(4, fn digit, _ -> segment_count(digit) == 4 end)
    |> solve_digit(8, fn digit, _ -> segment_count(digit) == 7 end)
    |> solve_digit(3, fn digit, current ->
      segment_count(digit) == 5 and subset?(get_value(current, 1), digit)
    end)
    |> solve_digit(9, fn digit, current ->
      segment_count(digit) == 6 and subset?(get_value(current, 3), digit)
    end)
    |> solve_digit(0, fn digit, current ->
      segment_count(digit) == 6 and subset?(get_value(current, 1), digit)
    end)
    |> solve_digit(6, fn digit, _ -> segment_count(digit) == 6 end)
    |> solve_digit(5, fn digit, current ->
      segment_count(digit) == 5 and subset?(digit, get_value(current, 6))
    end)
    |> solve_digit(2, fn digit, _ -> segment_count(digit) == 5 end)
    |> decode_display()
  end

  def solve_digit(%__MODULE__{} = entry, value, criteria) do
    digit = Enum.find(entry.digits, fn digit -> criteria.(digit, entry) end)

    record_solution(entry, digit, value)
  end

  def record_solution(%__MODULE__{} = entry, solved_digit, value) do
    %{
      entry
      | digits: Enum.filter(entry.digits, fn digit -> !MapSet.equal?(digit, solved_digit) end),
        solutions: Map.put(entry.solutions, solved_digit, value)
    }
  end

  def segment_count(digit), do: MapSet.size(digit)

  def subset?(child, parent), do: MapSet.subset?(child, parent)

  def get_value(entry, value) do
    entry.solutions
    |> Enum.find(fn {_digit, val} -> val == value end)
    |> elem(0)
  end

  def decode_display(%__MODULE__{display: display, solutions: solutions}) do
    display
    |> Enum.map(&Map.get(solutions, &1))
    |> Enum.join("")
    |> String.to_integer()
  end

  def parse_line(line) do
    [digits, display] =
      line
      |> String.split(" | ")
      |> Enum.map(&parse_digits/1)

    %__MODULE__{digits: digits, display: display}
  end

  def parse_digits(group) do
    group
    |> String.split(" ", trim: true)
    |> Enum.map(fn string -> string |> String.to_charlist() |> MapSet.new() end)
  end
end

SevenSegment.puzzle1(sample)
```

```elixir
SevenSegment.puzzle1(input)
```

## Puzzle 2

Through a little deduction, you should now be able to determine the remaining digits. Consider again the first example above:

acedgfb cdfbe gcdfa fbcad dab cefabd cdfgeb eafb cagedb ab |
cdfeb fcadb cdfeb cdbaf
After some careful analysis, the mapping between signal wires and segments only make sense in the following configuration:

```
 dddd
e    a
e    a
 ffff
g    b
g    b
 cccc
```

So, the unique signal patterns would correspond to the following digits:

```
acedgfb: 8
cdfbe: 5
gcdfa: 2
fbcad: 3
dab: 7
cefabd: 9
cdfgeb: 6
eafb: 4
cagedb: 0
ab: 1
```

Then, the four digits of the output value can be decoded:

```
cdfeb: 5
fcadb: 3
cdfeb: 5
cdbaf: 3
```

Therefore, the output value for this entry is 5353.

Following this same process for each entry in the second, larger example above, the output value of each entry can be determined:

```
fdgacbe cefdb cefbgd gcbe: 8394
fcgedb cgb dgebacf gc: 9781
cg cg fdcagb cbg: 1197
efabcd cedba gadfec cb: 9361
gecf egdcabf bgf bfgea: 4873
gebdcfa ecba ca fadegcb: 8418
cefg dcbef fcge gbcadfe: 4548
ed bcgafe cdgba cbgef: 1625
gbdfcae bgc cg cgb: 8717
fgae cfgab fg bagce: 4315
```

Adding all of the output values in this larger example produces 61229.

For each entry, determine all of the wire/segment connections and decode the four-digit output values. What do you get if you add up all of the output values?

### Just thinking here...

* 1 is the only digit with 2 segments
* 7 is the only digit with 3 segments
* 4 is the only digit with 4 segments
* 8 is the only digit with 7 segments
* 3 is the 5-segment digit that shares 2 letters with 1
* 9 is the 6-segment digit that contains all of the segments of 3
* 0 is the 6-segment digit that isn't 9 and contains both segments of 1
* 6 is the remaining 6-segment digit
* 5 is the 5-segment digit that isn't 3 and whose segments are contained in the segments of 6
* 2 is the remaining 5-segment digit

so we need to

* maintain a list of solved number -> digit mappings
* maintain a list of digit codes that haven't been mapped yet
* a function to determine if one digit's segments are a subset of another's
* a way to find by segment count and ^

Probably makes sense to store each of these as a MapSet instead of the characters

```elixir
SevenSegment.puzzle2(sample)
```

```elixir
SevenSegment.puzzle2(input)
```
